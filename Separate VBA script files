# VBA-challenge
   Sub Multiple_Year_Stock_data()

    Dim ticker_symbol As String
    Dim Next_ticker_symbol As String
    Dim quartly_change As Double
    Dim percentage As Double
    Dim TotalVolume As Double
    Dim openingPrice As Double
    Dim closingPrice As Double
    Dim ws As Worksheet
    
    ' Variables to store the greatest values
    Dim GreatestPercentIncrease As Double
    Dim GreatestPercentDecrease As Double
    Dim GreatestTotalVolume As Double
    Dim TickerGreatestIncrease As String
    Dim TickerGreatestDecrease As String
    Dim TickerGreatestVolume As String
    
    ' Initialize greatest values
    GreatestPercentIncrease = -999999
    GreatestPercentDecrease = 999999
    GreatestTotalVolume = 0
    
    ' Loop through each worksheet
    For Each ws In Worksheets
        RowCount = ws.Cells(Rows.Count, "A").End(xlUp).Row ' Count the number of rows
        j = 2 ' Start at row 2 for the summary
        
        ' Initialize the opening price
        openingPrice = ws.Cells(2, 3).Value
        
        ' Reset total volume for each worksheet
        TotalVolume = 0
        
        For i = 2 To RowCount
            ticker_symbol = ws.Cells(i, 1).Value
            Next_ticker_symbol = ws.Cells(i + 1, 1).Value
            
            ' Accumulate total volume
            TotalVolume = TotalVolume + ws.Cells(i, 7).Value
            
            ' Check if the next ticker symbol is different (or if we are at the last row)
            If ticker_symbol <> Next_ticker_symbol Or i = RowCount Then
                ' Set the closing price
                closingPrice = ws.Cells(i, 6).Value
                
                ' Calculate the quarterly change
                quartly_change = closingPrice - openingPrice
                
                ' Calculate Percentage
                If openingPrice <> 0 Then
                    percentage = (quartly_change / openingPrice) * 100
                Else
                    percentage = 0
                End If
                
                ' Output the results
                ws.Cells(j, 9).Value = ticker_symbol
                ws.Cells(j, 10).Value = quartly_change
                ws.Cells(j, 11).Value = percentage
                ws.Cells(j, 12).Value = TotalVolume
                
                ' Check for greatest percent increase
                If percentage > GreatestPercentIncrease Then
                    GreatestPercentIncrease = percentage
                    TickerGreatestIncrease = ticker_symbol
                End If
                
                ' Check for greatest percent decrease
                If percentage < GreatestPercentDecrease Then
                    GreatestPercentDecrease = percentage
                    TickerGreatestDecrease = ticker_symbol
                End If
                
                ' Check for greatest total volume
                If TotalVolume > GreatestTotalVolume Then
                    GreatestTotalVolume = TotalVolume
                    TickerGreatestVolume = ticker_symbol
                End If
                
                ' Reset total volume and set the opening price for the next ticker
                TotalVolume = 0
                j = j + 1
                If i <> RowCount Then
                    openingPrice = ws.Cells(i + 1, 3).Value
                End If
            End If
        Next i
    Next ws
    
    ' Output the greatest values
    Worksheets(1).Cells(2, 15).Value = "Greatest % Increase"
    Worksheets(1).Cells(2, 16).Value = TickerGreatestIncrease
    Worksheets(1).Cells(2, 17).Value = Format(GreatestPercentIncrease, "0.00") & "%"
    
    Worksheets(1).Cells(3, 15).Value = "Greatest % Decrease"
    Worksheets(1).Cells(3, 16).Value = TickerGreatestDecrease
    Worksheets(1).Cells(3, 17).Value = Format(GreatestPercentDecrease, "0.00") & "%"
    
    Worksheets(1).Cells(4, 15).Value = "Greatest Total Volume"
    Worksheets(1).Cells(4, 16).Value = TickerGreatestVolume
    Worksheets(1).Cells(4, 17).Value = Format(GreatestTotalVolume, "0.00E+00")

End Sub



